<!DOCTYPE html>
<!--[if lt IE 7]>      <html class="no-js lt-ie9 lt-ie8 lt-ie7"> <![endif]-->
<!--[if IE 7]>         <html class="no-js lt-ie9 lt-ie8"> <![endif]-->
<!--[if IE 8]>         <html class="no-js lt-ie9"> <![endif]-->
<!--[if gt IE 8]><!--> <html class="no-js"> <!--<![endif]-->
    <head>
        <meta charset="utf-8">
        <meta http-equiv="X-UA-Compatible" content="IE=edge">
        <title></title>
        <meta name="description" content="">
        <meta name="viewport" content="width=device-width, initial-scale=1">
    	<style>
    		html, body, canvas { 
    			width: 100%; 
    			height: 100%; 
    			margin: 0px; 
    			padding: 0px; 
    			position: absolute; 
    			top: 0px; 
    			left: 0px;
    			background: #555;
    		}
    	</style> 
    </head> 
    <body>
    	<script id="vs-plot" type="x-shader/x-vertex">
	    	attribute vec2 aTexCoord;
			attribute float aLineEnd;

			uniform mat4 uMVMatrix;
			uniform mat4 uPMatrix;
			uniform sampler2D uState;
			uniform float uResolution;
			uniform float uPointSize;
			uniform float uFogStart;
			uniform float uFogEnd;

			varying vec3 vColor;

			vec3 convertHSVToRGB(vec3 hsv) {
			  // Conversion per http://en.wikipedia.org/wiki/HSL_and_HSV#Converting_to_RGB
			  float h = fract(hsv[0]);
			  float s = hsv[1];
			  float v = hsv[2];
			  float c = s * v;
			  float hp = h * 6.0;
			  float x = c * (1.0 - abs(mod(hp, 2.0) - 1.0));
			  vec3 rgbp = hp < 1.0 ? vec3(c, x, 0.0) :
			              hp < 2.0 ? vec3(x, c, 0.0) :
			              hp < 3.0 ? vec3(0.0, c, x) :
			              hp < 4.0 ? vec3(0.0, x, c) :
			              hp < 5.0 ? vec3(x, 0.0, c) :
			                         vec3(c, 0.0, x);
			  return rgbp + v - c;
			}

			void main(void) {
			  // Retrieve particle state
			  vec4 state1 = texture2D(uState, aTexCoord);
			  vec4 state2 = texture2D(uState, aTexCoord + vec2(1.0/uResolution, 0.0));
			  // Untangle
			  vec3 pos = state1.xyz;
			  vec3 vel = state2.xyz;
			  float hue = state1.w;
			  float saturation = state2.w;
			  
			  pos += aLineEnd * vel;
			  
			  vec4 eyePosition = uMVMatrix * vec4(pos, 1.0);
			  gl_Position = uPMatrix * eyePosition;
			  vColor = convertHSVToRGB(vec3(hue, saturation, 1.0));
			  
			  // Fog
			  vColor = mix(vColor, vec3(0.0), clamp((-eyePosition.z - uFogStart) / (uFogEnd - uFogStart), 0.0, 1.0));
			  
			  // Compute perspective point size
			  vec4 projSize = uPMatrix * vec4(uPointSize, 0.0, eyePosition.zw);
			  gl_PointSize = projSize.x / projSize.w;
			}
    	</script>

    	<script id="fs-plot" type="x-shader/x-fragment">
	    	#ifdef GL_ES
			precision highp float;
			#endif

			varying vec3 vColor;

			void main(void) {
			  vec3 color = vColor;
			  
			#if SHAPE_SOFT
			    color *= max(0.0, 1.0 - 2.0 * length(gl_PointCoord - 0.5));
			#endif

			  gl_FragColor = vec4(color, 1.0);
			}
    	</script>


    	<script id="vs-update" type="x-shader/x-vertex">
	    	attribute vec2 aPosition;

			void main(void) {
			  gl_Position = vec4(aPosition, 0.0, 1.0);
			}
    	</script>

    	<script id="fs-update" type="x-shader/x-fragment">
    		#ifdef GL_ES
			precision highp float;
			#endif

			uniform sampler2D uState;
			uniform float uResolution;
			uniform float uDT;
			uniform float uHarmonicAccel;

			vec4 lookup(float offset) {
			  return texture2D(uState, (gl_FragCoord.xy + vec2(offset, 0.0)) / uResolution);
			}

			void main(void) {
			  bool updatingPosition = mod(gl_FragCoord.x, 2.0) < 1.0;
			  vec4 posp, velp;
			  if (updatingPosition) {
			    posp = lookup(0.0);
			    velp = lookup(1.0);
			  } else {
			    posp = lookup(-1.0);
			    velp = lookup(0.0);
			  }
			  vec3 pos = vec3(posp);
			  vec3 vel = vec3(velp);
			  
			  // velocity update is applied to position as well
			  vel += uHarmonicAccel * uDT * normalize(pos);
			  
			  if (updatingPosition) {
			    gl_FragColor = vec4(pos + uDT * vel, posp[3]);
			  } else {
			    gl_FragColor = vec4(vel, velp[3]);
			  }
			}
    	</script>

       <script src="three.min.js"></script>
       <script src="main.js"></script>
    </body>
</html>